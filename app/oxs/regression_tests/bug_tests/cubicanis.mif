# MIF 2.1
# MIF Test File: cubicanis.mif
# Description: Regression test file for Oxs_CubicAnisotropy class.

set pi [expr 4*atan(1.0)]
set mu0 [expr 4*$pi*1e-7]

RandomSeed 1

Parameter xcount 55
Parameter ycount 37
Parameter zcount 12

set K1 1e6

set xcellsize 3e-9
set ycellsize 5e-9
set zcellsize 7e-9

Specify Oxs_BoxAtlas:atlas [subst {
  xrange {0 [expr {$xcount*$xcellsize}]}
  yrange {0 [expr {$ycount*$ycellsize}]}
  zrange {0 [expr {$zcount*$zcellsize}]}
}]

Specify Oxs_RectangularMesh:mesh [subst {
  cellsize {$xcellsize $ycellsize $zcellsize}
  atlas :atlas
}]

Specify Oxs_RandomVectorField:axis1 [subst {
   min_norm 1.0
   max_norm 1.0
   cache_grid :mesh
}]

Specify Oxs_CubicAnisotropy [subst {
  K1 { Oxs_RandomScalarField {
     range_min [expr {-1*abs($K1)}]
     range_max [expr {abs($K1)}]
  }}
  axis1 :axis1
  axis2 { Oxs_PlaneRandomVectorField {
     plane_normal :axis1
     min_norm 1.0
     max_norm 1.0
     cache_grid :mesh
  }}
}]

set Ms 800e3
set A 13e-12

Specify Oxs_UniformExchange [subst {
  A $A
}]

Specify Oxs_EulerEvolve:evolve {}

proc Ellipsoid { value x y z } {
   set x [expr {2.*$x-1.}]
   set y [expr {2.*$y-1.}]
   set z [expr {2.*$z-1.}]
   if {$x*$x+$y*$y+$z*$z>=1} {
      return 0.0
   }
   return $value
}

proc Texture {} {
   set weight 0.25
   set tx [expr {2*rand()-1.0}]
   set ty [expr {2*rand()-1.0}]
   set tz [expr {2*rand()-1.0}]
   set x [expr {3*(1.0 + $weight*$tx)}]
   set y [expr {2*(1.0 + $weight*$ty)}]
   set z [expr {1.0 + $weight*$tz}]
   return [list $x $y $z]
}

Specify Oxs_TimeDriver [subst {
 basename cubicanis
 evolver :evolve
 stopping_dm_dt 0.1
 total_iteration_limit 10
 mesh :mesh
 Ms { Oxs_ScriptScalarField {
    script "Ellipsoid $Ms"
    script_args relpt
    atlas :atlas
 }}
 m0 { Oxs_ScriptVectorField {
    script Texture
    script_args {}
    atlas :atlas
    norm 1.0
 }}
}]

Destination archive mmArchive
Schedule DataTable archive Step 1
