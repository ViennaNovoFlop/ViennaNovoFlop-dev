# MIF 2.1
# MIF Test File: twosurface.mif
# Description: Test of  anti-ferromagnetic exchange
#  coupling between adjacent layers.
#
set pi [expr 4*atan(1.0)]
set mu0 [expr 4*$pi*1e-7]

Parameter sigma2 0.0

Specify Oxs_MultiAtlas:atlas {
    atlas { Oxs_BoxAtlas {
	name top
	xrange {0 400e-9}
	yrange {0 200e-9}
	zrange {3e-9 6e-9}
    } }
    atlas { Oxs_BoxAtlas {
        name bottom
        xrange {0 400e-9}
        yrange {0 200e-9}
        zrange {0 3e-9}
    } }
}

Specify Oxs_RectangularMesh:mesh {
  cellsize {5e-9 5e-9 3e-9}
  atlas :atlas
}

Specify Oxs_Exchange6Ngbr {
  atlas :atlas
  default_A 30e-12
  A  {
    top bottom 0
  }
}

# Add 50 mT (approx. 40 kA/m) biasing field to bottom layer.
Specify Oxs_FixedZeeman:Bias [subst {
  multiplier [expr {0.001/$mu0}]
  field { Oxs_AtlasVectorField {
    atlas :atlas
    default_value {0. 0. 0.}
    values {
	bottom  {50 0. 0.}
    }
  }}
}]

# Add antiferromagnetic exchange coupling across top-bottom layers.  The
# sign and strength of the bilinear coupling is determined by the value
# of "sigma".  Use non-zero sigma2 to enable biquadratic coupling.
if {$sigma2 != 0.0} {
   set biquad "sigma2 $sigma2"
} else {
  set biquad {}
}
Specify Oxs_LinearScalarField:zheight {
 vector {0 0 1}
 norm   1.0
}
Specify Oxs_TwoSurfaceExchange:AF [subst {
 sigma -1e-4
 $biquad
 surface1 {
         atlas  :atlas
        region  bottom
   scalarfield  :zheight
   scalarvalue  3e-9
    scalarside  -
 }
 surface2 {
         atlas  :atlas
        region  top
   scalarfield  :zheight
   scalarvalue  3e-9
    scalarside  +
 }
 report_max_spin_angle 1
}]

# Stepped applied field
Specify Oxs_UZeeman [subst {
  multiplier [expr 0.001/$mu0]
  Hrange {
     {    0  0  0   500  5  0   1 }
     {  500  5  0  -500 -5  0   1 }
     { -500 -5  0   500  5  0   1 }
  }
}]

Specify Oxs_Demag {}

Specify Oxs_RungeKuttaEvolve:evolve {
  alpha 0.5
}

Specify Oxs_TimeDriver {
 evolver :evolve
 stopping_dm_dt .01
 stage_iteration_limit 10
 mesh :mesh
 Ms  { Oxs_AtlasScalarField {
     atlas :atlas
     default_value 0
     values {
        top 1400e3
        bottom 1400e3
     }
 }}
 m0 {10 1 0}
 report_max_spin_angle 1
}

Destination archive mmArchive
Schedule DataTable archive Step 1
